apply plugin: "org.jetbrains.dokka"
apply plugin: "com.github.jruby-gradle.base"

dependencies {
  jrubyExec "rubygems:jekyll:3.8.2"
  jrubyExec "rubygems:jekyll-asciidoc:2.1.0"
  jrubyExec "rubygems:minima:2.5.0"
  jrubyExec "rubygems:jekyll-theme-hydeout:3.6.0"
}

def jekyllWorkDir = "${projectDir}/src/main/jekyll"

task "jekyllInit"(type: com.github.jrubygradle.JRubyExec) {
  script "jekyll"
  scriptArgs "new", jekyllWorkDir
}

task jekyllServe(type: com.github.jrubygradle.JRubyExec) {
  script "jekyll"
  workingDir jekyllWorkDir
  scriptArgs "serve"
}

dokka {
  // I don't know why this task always thinks it's up to date
  outputs.upToDateWhen { false }
  outputFormat = "jekyll"
  moduleName = "strikt-core"
  jdkVersion = 8
  kotlinTasks {
    [project(":strikt-core").compileKotlin]
  }
  samples = ["$rootDir/samples/src/test/kotlin"]
}

task copyApiDocs(type: Copy) {
  outputs.upToDateWhen { false }
  from(dokka)
  into("${jekyllWorkDir}/api/")
  filter { line ->
    line.find(/title: (.+)/) { l, t -> "$l\nlayout: page\nmenu: false" } ?: line
  }
}

jekyllServe.dependsOn(copyApiDocs)
